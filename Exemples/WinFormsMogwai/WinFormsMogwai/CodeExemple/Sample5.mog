mogwai.reset

forever do
{
	cls

	"*** FIND THE NUMBER BETWEEN 1 AND 100 ***" ?
	" " ?

	rand 100 * ->int 1 + -> 'number'
	0 -> 'counter' 
	
	forever do
	{
		# Enter the number; it must be between 1 and 100.
		
		forever do
		{
			"Your number: " prompt -> 'r'
			" " ?

			guard
			{
				r ->num -> 'r'
				
				if (r 0 > r 101 < and) then
				{
					'counter' ++
					break
				}
				else
				{
					"The number must be between 1 and 100, please try again." ?
					" " ?
				}
			}
			else
			{
				"Incorrect entry, please try again." ?
				" " ?
			}	
		}
		
		# We test if the number is too small, too large, or just right.
		
		switch
		{
			(r number >) 
			{
				"{! r} is too big." eval ?
			}
			
			(r number <)
			{
				"{! r} is too small." eval ?
			}
			
			(true)
			{
				"Well done, you found it in {! counter} tries!" eval ?
				break
			}
		}
		
		" " ?
	}
	
	" " ?
	"Do you want to play again? (Y/N): " prompt ->upper -> 'r'
	
	if (("N" "NO") r contains) then 
	{ 
		break 
	}
}

